###########################################################################
# Build the npm libraries: metapage, metaframe
###########################################################################
# just configuration
###########################################################################
set shell          := ["bash", "-c"]
set dotenv-load    := true
###########################################################################
# library configuration
###########################################################################
NPM_MODULE         := `cat package.json | jq -r .name`
tsc                := "./node_modules/typescript/bin/tsc"
vite               := "./node_modules/vite/bin/vite.js"
# minimal formatting, bold is very useful
bold               := '\033[1m'
normal             := '\033[0m'
green              := "\\e[32m"
yellow             := "\\e[33m"
blue               := "\\e[34m"
magenta            := "\\e[35m"
grey               := "\\e[90m"

_help:
    @just --list --unsorted --list-heading $'Commands:\n'

# Build the production npm distributions in dist/metaframe and dist/metapage
build: _build
_build: _ensure_npm_modules _build_npm

_build_npm:
    #!/usr/bin/env bash
    set -euo pipefail
    # Compiles the entire codebase to typescript files in ./dist. Packaged into the npm module '{{NPM_MODULE}}'
    # How to test installation of the build?
    {{tsc}} --noEmit
    echo "‚úÖ typescript check"
    # Set NODE_OPTIONS as env var for CI compatibility (prevents hanging)
    export NODE_OPTIONS='--max_old_space_size=16384'
    # Ensure CI mode is set (prevents watch mode and other interactive behaviors)
    export CI=true
    echo "üî® Starting vite build..."
    # Use explicit mode for CI and ensure stdin is closed (prevents hanging in CI)
    # The force-exit plugin in vite.config.ts will ensure the process exits properly
    {{vite}} build --mode production < /dev/null
    echo "‚úÖ vite build"

# watch for file changes, then build ALL into ./dist
watch: _ensure_npm_modules
    #!/usr/bin/env bash
    set -euo pipefail
    echo "üëÄ watching and building into ./dist..."
    {{tsc}} --noEmit
    echo "‚úÖ typescript check"
    {{vite}} --watch build

@_tsc +args="":
    {{tsc}} {{args}}

fmt +failIfChanged="":
    #!/usr/bin/env bash
    set -euo pipefail
    echo -e "TODO: npm run lint:fix"
    npm run format
    if [ "{{ failIfChanged }}" != "" ]; then
        if ! git diff --quiet; then
            echo "üí• Files were modified by formatting"
            git diff --name-only
            echo "üí• üëâ Run: 'just frontend/browser/format'"
            exit 1
        fi
    fi

# check
@test-io: 
    npm run test:browser --browser=chromium

@test-io-watch: _ensure_npm_modules
    npm run test-watch

# Run tests
test:
    just ../worker/test

# Develop:
#   1. just dev
#   2. modify metapage/metaframe code
#   3. refresh browser window
# recompile metapage/metaframe src on change, and open a browser at the test page for the local metapage test
dev: _ensure_npm_modules watch

# typescript check 
@check: (_tsc "--build")
    echo -e "‚úÖ {{green}}TypeScript{{normal}} check passed"

# npm link the package in dist for local development. In the other project: 'npm link @metapages/metapage'
@link:
    if [ ! -d dist ]; then just build; fi
    cd dist && npm link
    echo -e "üëâ in the other project: npm link {{NPM_MODULE}}"

# unlink the package in dist from local development. You probably don't ever need to do this
@unlink:
    cd dist && npm unlink

# List all published versions
@list:
    npm view {{NPM_MODULE}} versions --json

# Verify package is ready for publishing (checks dist/, npm pack, and jsDelivr bundling)
verify: _ensure_npm_modules build
    #!/usr/bin/env bash
    set -euo pipefail
    echo "üîç Running pre-publish verification..."
    node test-jsdelivr-bundle.mjs

# If the version does not exist, publish the packages (metaframe+metapage)
publish: _ensure_npm_modules
    #!/usr/bin/env bash
    set -euo pipefail
    VERSION=`cat package.json | jq -r '.version'`
    # Check if the package exists, if not this is the first publish
    if ! npm view {{NPM_MODULE}} version &> /dev/null; then
        echo "üì¶ First time publishing {{NPM_MODULE}}"
    else
        INDEX=`npm view {{NPM_MODULE}} versions --json | jq "index( \"$VERSION\" )"`
        if [ "$INDEX" != "null" ]; then
            echo -e 'üå≥ Version exists, not publishing'
            exit 0
        fi
    fi
    just build
    # Verify the build before publishing
    echo "üîç Running pre-publish verification..."
    node test-jsdelivr-bundle.mjs
    echo "PUBLISHING npm version $VERSION"
    # OIDC Trusted Publishing (automatically uses GitHub Actions OIDC token)
    npm publish --access public .
    git tag "v$VERSION"
    git push origin "v$VERSION"

# Unpublish version https://docs.npmjs.com/cli/v7/commands/npm-unpublish
unpublish version:
    @echo "‚ùó If this fails: you cannot use .npmrc or NPM_TOKEN, you must 'npm login' ü§∑‚Äç‚ôÄÔ∏è"
    npm unpublish {{NPM_MODULE}}@{{version}}

# https://docs.npmjs.com/cli/v7/commands/npm-deprecate
module_deprecate version +message:
    npm deprecate {{NPM_MODULE}}@{{version}} "{{message}}"

# delete all generated assets/files
@clean:
    mkdir -p dist
    rm -rf dist/*
    echo -e "‚úÖ Cleaned libs"

@_ensure_npm_modules:
    if [ ! -d node_modules ]; then npm i; fi
